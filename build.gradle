plugins {
  id "java-library"
  id "com.github.johnrengelman.shadow" version "5.2.0"
}

group = "com.mineteria"
version = "0.1.0-SNAPSHOT"
description = "Allows the application of mixins provided by plugins on Mineteria."

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    url "https://repo.spongepowered.org/maven/"
  }
  maven {
    url "https://files.minecraftforge.net/maven/"
  }
}

dependencies {
  api "org.checkerframework:checker-qual:3.7.0"
  api "org.spongepowered:mixin:0.8.2"
  api "cpw.mods:modlauncher:8.0.6"
}

tasks.withType(JavaCompile) {
  options.compilerArgs += ["-Xlint:all", "-Xlint:-path", "-parameters"]
  options.deprecation = true
  options.encoding = "UTF-8"
}

jar {
  manifest.attributes(
    "Specification-Title": "MixinBootstrapper",
    "Specification-Vendor": "Mineteria",
    "Specification-Version": 1,
    "Implementation-Title": project.name,
    "Implementation-Version": project.version
  )
}

javadoc {
  options.encoding = "UTF-8"
  options.charSet = "UTF-8"
  options.links(

  )

  // Disable the crazy super-strict doclint tool in Java 8
  options.addStringOption("Xdoclint:none", "-quiet")
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  classifier = 'sources'
}

task javadocsJar(type: Jar) {
  from javadoc
  classifier = "javadoc"
}

shadowJar {
  classifier = "shaded"
}
